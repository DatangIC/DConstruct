plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.protobuf'
}

def config = rootProject.ext

android {
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main {
            proto {
                srcDir 'src/main/java/proto'
                include '**/*.proto'
            }
        }
    }
    namespace 'com.datangic.data'
}

dependencies {

    kapt config.kapt.room

    implementation config.androidx.lifecycle_livedata
    implementation config.google.gson

    api config.room.runtime
    api config.room.ktx

    // DataStore
    //noinspection GradleDependency
    api config.androidx.datastore
    api config.androidx.datastore_preferences
    api config.google.protobuf

    api project(":libs:api")
    implementation project(":libs:common")

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.4"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}